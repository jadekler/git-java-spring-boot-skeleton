import org.gradle.logging.StyledTextOutputFactory
import static org.gradle.logging.StyledTextOutput.Style

apply plugin: 'java'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE'
    }
}

repositories {
    mavenCentral()
}

String springVersion = '4.2.1.RELEASE'
String springBootVersion = '1.2.5.RELEASE'
String seleniumVersion = '2.47.1'
String fluentleniumVersion = '0.10.3'

dependencies {
    compile (
        project(':client'),

        "org.springframework:spring-core:$springVersion",
        "org.springframework:spring-web:$springVersion",
        "org.springframework:spring-webmvc:$springVersion",

        "org.springframework.boot:spring-boot-starter:$springBootVersion",
        "org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion",
        "org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion",
    )

    testCompile (
        'junit:junit',
        "org.springframework:spring-test:$springVersion",

        'com.codeborne:phantomjsdriver:1.2.1',

        "org.seleniumhq.selenium:selenium-java:$seleniumVersion",
        "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion",

        "org.fluentlenium:fluentlenium-core:$fluentleniumVersion",
        "org.fluentlenium:fluentlenium-assertj:$fluentleniumVersion",
    )

    configurations {
        testCompile.exclude group: 'org.seleniumhq.selenium', module: 'selenium-chrome-driver'
        testCompile.exclude group: 'org.seleniumhq.selenium', module: 'selenium-edge-driver'
        testCompile.exclude group: 'org.seleniumhq.selenium', module: 'selenium-htmlunit-driver'
        testCompile.exclude group: 'org.seleniumhq.selenium', module: 'selenium-firefox-driver'
        testCompile.exclude group: 'org.seleniumhq.selenium', module: 'selenium-ie-driver'
        testCompile.exclude group: 'org.seleniumhq.selenium', module: 'selenium-safari-driver'
    }
}

System.setProperty("org.gradle.color.failure", "RED")
System.setProperty("org.gradle.color.progressstatus", "YELLOW")
System.setProperty("org.gradle.color.success", "GREEN")

test {
    def out = services.get(StyledTextOutputFactory).create("colored-test-output")
    out.style(Style.Normal)

    beforeSuite { suite ->
        if (suite.name.startsWith("Test Run") || suite.name.startsWith("Gradle Worker")) return
        out.println("\n" + suite.name)
    }
    afterTest { descriptor, result ->
        def style
        if (result.failedTestCount > 0) style = Style.Failure
        else if (result.skippedTestCount > 0) style = Style.ProgressStatus
        else style = Style.Success

        out.text('  ').withStyle(style).println(descriptor.name)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

defaultTasks 'build'